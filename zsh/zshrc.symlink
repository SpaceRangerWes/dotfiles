# -*- mode: sh -*-

function {
  export DOTFILES="${HOME}/.dotfiles"
  export PROJECTS="${HOME}/Projects"
  export JAVA_HOME="$(/usr/libexec/java_home -v 1.8)"

  #
  # Zprezto
  #

  # Load zprezto if it's installed
  if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
  fi

  #
  # Zsh submodules
  #

  # All zsh config files (all *.zsh files contained in $DOTFILES children
  # directories). Note that this glob is limited to one directory level deep.
  local -a config_files
  config_files=(${DOTFILES}/*/*.zsh)

  # Load path files
  for file in ${(M)config_files:#*/path.zsh}; do
    source $file
  done

  # Load all files except for path, and completions
  for file in ${config_files:#*/(path.zsh|completion.zsh)}; do
    source $file
  done

  # Load completions
  for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
  done
}

export NVM_DIR="~/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm
